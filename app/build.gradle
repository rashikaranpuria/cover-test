apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

def getPlacesAPIKey() {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    return properties.getProperty("PLACES_API_KEY")
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.rashikaranpuria.covertest"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "PLACES_API_KEY", "\"" + getPlacesAPIKey() + "\""
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    lintOptions {
        abortOnError false
    }
}
configurations {
    ktlint
}

dependencies {
    def anko_version = "0.10.4"
    def dagger_version = "2.17"
    def rxkotlin_version = "2.1.0"
    def rxandroid_version = "2.1.0"
    def robolectric_version = "3.8"
    def mockito_version = "2.22.0"
    def retrofit_version = "2.4.0"
    def rxjava_adapter_version = "1.0.0"
    def ktlint_version = "0.28.0"
    def junit_version = "4.12"
    def test_runner_version = "1.0.2"
    def rxbinding_version = "2.1.1"
    def javax_annotation_version = "1.0"
    def android_support_version = "28.0.0"
    def constraint_layout_version = "1.1.3"

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "com.android.support:appcompat-v7:$android_support_version"
    implementation "com.android.support:recyclerview-v7:$android_support_version"
    implementation "com.android.support.constraint:constraint-layout:$constraint_layout_version"

    // Anko
    implementation "org.jetbrains.anko:anko:$anko_version"

    // Dagger
    implementation "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    compileOnly "javax.annotation:jsr250-api:$javax_annotation_version"

    // ReactiveX + RxBindings
    implementation "io.reactivex.rxjava2:rxkotlin:$rxkotlin_version"
    implementation "io.reactivex.rxjava2:rxandroid:$rxandroid_version"
    implementation "com.jakewharton.rxbinding2:rxbinding:$rxbinding_version"

    // Retrofit + Network
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.jakewharton.retrofit:retrofit2-rxjava2-adapter:$rxjava_adapter_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"

    // ktlint
    ktlint "com.github.shyiko:ktlint:$ktlint_version"

    // Testing
    testImplementation "junit:junit:$junit_version"
    androidTestImplementation "com.android.support.test:runner:$test_runner_version"
    testImplementation "org.mockito:mockito-core:$mockito_version"
    testImplementation "org.robolectric:robolectric:$robolectric_version"
}
task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "src/**/*.kt"
    //args "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
}
check.dependsOn ktlint
task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "-F", "src/**/*.kt"
}
